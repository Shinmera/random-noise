(in-package #:org.shirakumo.random-noise)

(declaim (inline square-vectors octahedron-vectors line square circle octahedron sphere))
(defun square-vectors (xxhash)
  (declare (type xxhash xxhash))
  (let* ((c0 (1- (* 2 (xxhash-float xxhash))))
         (c1 (- 0.5f0 (abs c0)))
         (c0 (- c0 (floor (+ 0.5 c0)))))
    (values c0 c1)))

(defun octahedron-vectors (xxhash)
  (declare (type xxhash xxhash))
  (let* ((c0 (1- (* 2 (xxhash-float xxhash 0))))
         (c1 (1- (* 2 (xxhash-float xxhash 3))))
         (c2 (- 1 (abs c0) (abs c1)))
         (offset (max 0f0 (- c2)))
         (c0 (+ c0 (if (< c0 0) offset (- offset))))
         (c1 (+ c1 (if (< c1 0) offset (- offset)))))
    (values c0 c1 c2)))

(defun line (xxhash x)
  (declare (type xxhash xxhash))
  (declare (type single-float x))
  (let ((l (* (1+ (xxhash-float xxhash))
              (if (= 0 (ldb (byte 1 8) (xxhash-int xxhash)))
                  +1f0 -1f0))))
    (sample (* l x) l)))

(defun square (xxhash x y)
  (declare (type xxhash xxhash))
  (declare (type single-float x y))
  (multiple-value-bind (c0 c1) (square-vectors xxhash)
    (sample (+ (* c0 x) (* c1 y))
            c0
            c1)))

(defun circle (xxhash x y)
  (declare (type xxhash xxhash))
  (declare (type single-float x y))
  (multiple-value-bind (c0 c1) (square-vectors xxhash)
    (let ((r (/ (sqrt (+ (* c0 c0) (* c1 c1))))))
      (sample (* r (+ (* c0 x) (* c1 y)))
              (* r c0)
              (* r c1)))))

(defun octahedron (xxhash x y z)
  (declare (type xxhash xxhash))
  (declare (type single-float x y z))
  (multiple-value-bind (c0 c1 c2) (octahedron-vectors xxhash)
    (sample (+ (* c0 x) (* c1 y) (* c2 z))
            c0
            c1
            c2)))

(defun sphere (xxhash x y z)
  (declare (type xxhash xxhash))
  (declare (type single-float x y z))
  (multiple-value-bind (c0 c1 c2) (octahedron-vectors xxhash)
    (let ((r (/ (sqrt (+ (* c0 c0) (* c1 c1) (* c2 c2))))))
      (sample (* r (+ (* c0 x) (* c1 y) (* c2 z)))
              (* r c0)
              (* r c1)
              (* r c2)))))

(defun value-gradient/1d (xxhash x)
  (declare (type xxhash xxhash))
  (declare (type single-float x))
  (declare (ignore x))
  (sample (1- (* 2 (xxhash-float xxhash)))))

(defun value-gradient/2d (xxhash x y)
  (declare (type xxhash xxhash))
  (declare (type single-float x y))
  (declare (ignore x y))
  (sample (1- (* 2 (xxhash-float xxhash)))))

(defun value-gradient/3d (xxhash x y z)
  (declare (type xxhash xxhash))
  (declare (type single-float x y z))
  (declare (ignore x y z))
  (sample (1- (* 2 (xxhash-float xxhash)))))

(defun perlin-gradient/1d (xxhash x)
  (declare (type xxhash xxhash))
  (declare (type single-float x))
  (line xxhash x))

(defun perlin-gradient/2d (xxhash x y)
  (declare (type xxhash xxhash))
  (declare (type single-float x y))
  (s/ * (square xxhash x y)
      (/ 2f0 0.53528f0)))

(defun perlin-gradient/3d (xxhash x y z)
  (declare (type xxhash xxhash))
  (declare (type single-float x y z))
  (s/ * (octahedron xxhash x y z)
      (/ 1f0 0.56290f0)))

(defun simplex-gradient/1d (xxhash x)
  (declare (type xxhash xxhash))
  (declare (type single-float x))
  (s/ * (line xxhash x)
      (/ 32f0 27f0)))

(defun simplex-gradient/2d (xxhash x y)
  (declare (type xxhash xxhash))
  (declare (type single-float x y))
  (s/ * (circle xxhash x y)
      (/ 5.832f0 (sqrt 2f0))))

(defun simplex-gradient/3d (xxhash x y z)
  (declare (type xxhash xxhash))
  (declare (type single-float x y z))
  (s/ * (sphere xxhash x y z)
      (/ 1024f0 (* 125f0 (sqrt 3f0)))))
